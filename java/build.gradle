plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.5'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'jacoco'
}

group = 'ru.sng'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'javax.servlet:javax.servlet-api:3.0.1'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-json'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'com.h2database:h2'
	implementation 'javax.persistence:javax.persistence-api:2.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

jacoco {
	toolVersion = "0.8.11"
}

test {
	reports {
		html.enabled true
		html.destination file("${buildDir}/jacoco/jacocoHtml")
	}
	finalizedBy jacocoTestReport // report is always generated after tests run
	finalizedBy jacocoTestCoverageVerification
}

def jacocoExcluded = [
	"com/example/shortener/model/*",
	"com/example/shortener/messages/*",
	"com/example/shortener/ShortenerApplication*",
]

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: jacocoExcluded)
		}))
	}
}

jacocoTestCoverageVerification {
	dependsOn test
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: jacocoExcluded)
		}))
	}
	violationRules {
		rule {
			element = 'BUNDLE'
			limit {
				counter = 'INSTRUCTION'
				value = 'COVEREDRATIO'
				minimum = 0.7
			}
		}
	}
}